buildscript {
    repositories {
        mavenCentral()
    }

    ext {
        junitVersion = '5.9.2'
        lombokVersion = '1.18.26'
        ikonliVersion = '12.3.1'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.26.0'
}

repositories {
    mavenCentral()
}

project.description = "Evosim evolution simulator"
project.version = "1.0"

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'org.totallyspies.evosim'
    mainClass = 'org.totallyspies.evosim.ui.EvosimApplication'
}

run {
    jvmArgs = ['-Djdk.gtk.version=2']
}

javafx {
    version = '19.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    implementation group: 'org.json', name: 'json', version: '20230227'

    implementation 'com.google.guava:guava:31.0.1-jre'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.0'

    implementation "org.kordamp.ikonli:ikonli-swing:${ikonliVersion}"
    implementation "org.kordamp.ikonli:ikonli-javafx:${ikonliVersion}"
    implementation "org.kordamp.ikonli:ikonli-materialdesign2-pack:${ikonliVersion}"

    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
}

test {
    useJUnitPlatform()
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    imageName = 'evosim'

    launcher {
        name = 'evosimRun'
        jvmArgs = ['-Djdk.gtk.version=2']
        noConsole = true
    }
}
